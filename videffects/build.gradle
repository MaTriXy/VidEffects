apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

/**
 * This is needed because we want to compile this as android library so that we can test it before publishing
 */
android {

    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.3.0'
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            releaseLibrary(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.github.krazykira'
                artifactId = 'videffects'
                version = '1.1.1'
                pom {
                    name = 'VidEffects'
                    description = 'It is an Android library uses OpenGL Shaders to apply effects on Videos at Runtime.'
                    url = 'https://github.com/krazykira/VidEffects'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'krazykira'
                            name = 'Sheraz Ahmed Khilji'
                            email = 'sherazkhilji@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/krazykira/VidEffects.git'
                        developerConnection = 'scm:git:ssh://github.com/krazykira/VidEffects.git'
                        url = 'github.com/krazykira/VidEffects/'
                    }
                }
                repositories {
                    mavenCentral {
                        // Url for Staging Snapshot Deployment
//                      url "https://oss.sonatype.org/content/repositories/snapshots/"

                        // Url for Live/Release artifact Deployment
                        url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                        credentials {
                            username "$osshUsername"
                            password "$osshPassword"
                        }
                    }
                }
            }
        }
    }
/**
 * Steps for publishing the artifact
 * 1. For publishing. You need to have the public gpg key and gradle.properties.
 * 2. Once you have them available then you need to run ./gradlew publishReleaseLibraryPublicationToMavenRepoRepository
 * 3. Once the gradle task completes goto https://oss.sonatype.org/index.html#stagingRepositories and login with your credentials.
 * 4. After logging in, go to Staging repositories section and you will see the recently uploaded releasable artifact there.
 * 5. Select the artifact, then click close button. A few validations will run and if all of them pass then release button is enabled.
 * 6. Click the release button and the artifact will be released.
 */
    signing {
        sign publishing.publications.releaseLibrary
    }
}



